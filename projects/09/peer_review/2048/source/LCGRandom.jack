

class LCGRandom {
  static int seed;
  static int A;
  static int M;
  static int Q;
  static int R;
  
  function void setSeed(int newSeed) {
    let seed = newSeed;
    if (seed = 0) {
      let seed = 1;
    }
    let A=219;
    let M=32749;
    let Q=M/A;
    let R=Utils.mod(M,A);
    return;
  }
  
  // returns a random int in range 0..(M-1) inclusive
  function int rand() {
    var int test;
    let test=(A*(Utils.mod(seed,Q)))-(R*(seed/Q));
    if (test < 0) {
      let seed=test+M;
    } else {
      let seed=test;
    }
    return seed;
  }

  // returns a random int in range low..high inclusive
  function int randRange(int low, int high) {
    var int scale, rand;
    let scale = (M / (high - low + 1));
    let rand = (LCGRandom.rand() / scale) + low;
    while (rand > high) {
      let rand = (LCGRandom.rand() / scale) + low;
    }
    return rand;
  }
}
