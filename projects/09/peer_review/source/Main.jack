

class Main {

  // initialize random number generator
  function void startRandom() {
    var int count;
    do Output.moveCursor(11, 5);
    do Output.printString("Press and release any key");
    let count = 0;
    while (Keyboard.keyPressed() = 0) {
      let count = count + 1;
    }
    while (~(Keyboard.keyPressed() = 0)) {
      let count = count + 1;
    }
    do LCGRandom.setSeed(count);
    return;
  }

  // wait for key press 
  function char waitKey() {
    var int key;
    let key = Keyboard.keyPressed();
    while (key = 0) {
      let key = Keyboard.keyPressed();    
    }
    while (~(Keyboard.keyPressed() = 0)) {
    }
    return key;    
  }

  // returns number of points gained
  function int makeMove(Board b, char key) {
    if (key = 130) {
      return b.pushLeft();
    }
    if (key = 131) {
      return b.pushUp();
    }
    if (key = 132) {
      return b.pushRight();
    }
    if (key = 133) {
      return b.pushDown();
    }
    return 0;
  }

  function void game() {
    var Board b;
    var char key;
    var int points;

    let b = Board.new();
    let points = 0;
    while (true) {
      if (~b.insert()) {
	do Output.moveCursor(10, 3);
        do Output.printString("GAME");
	do Output.moveCursor(12, 3);
        do Output.printString("OVER");
	return;
      }
      do b.draw();
      do Output.moveCursor(2,1);
      do Output.printString("Points:");
      do Output.moveCursor(3,2);
      do Output.printInt(points);
      let key = Main.waitKey();
      let points = points + Main.makeMove(b, key);
    }
    return;
  }
  
  function void main() {
    do Main.startRandom();
    do Main.game();
    return;
  }
}
